extends CharacterBody3D

const SPEED = 5.0
const JUMP_VELOCITY = 4.5
const SENSITIVITY = 0.001
const TURN_SPEED = 50.0 # higher = snappier model turning

@onready var head = $Head
@onready var camera: Camera3D = $Head/SpringArm3D/Camera3D
@onready var model = $MeshInstance3D 

func _ready():
	Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)

func _unhandled_input(event):
	if event is InputEventMouseMotion:
		head.rotate_y(-event.relative.x * SENSITIVITY)

		camera.rotate_x(-event.relative.y * SENSITIVITY)
		camera.rotation.x = clamp(camera.rotation.x, deg_to_rad(-80), deg_to_rad(80))

func _physics_process(delta: float) -> void:
	# Gravity
	if not is_on_floor():
		velocity += get_gravity() * delta

	# Jump
	if Input.is_action_just_pressed("ui_accept") and is_on_floor():
		velocity.y = JUMP_VELOCITY

	# Input
	var inputDir: Vector2 = Input.get_vector("ui_left", "ui_right", "ui_up", "ui_down")

	# Camera-relative movement basis (flattened)
	var camForward: Vector3 = -camera.global_transform.basis.z
	camForward.y = 0.0
	camForward = camForward.normalized()

	var camRight: Vector3 = camera.global_transform.basis.x
	camRight.y = 0.0
	camRight = -camRight.normalized()

	# Move direction based on camera
	var direction: Vector3 = -(camRight * inputDir.x + camForward * inputDir.y)
	direction.y = 0.0
	direction = direction.normalized()

	# Apply movement
	if direction != Vector3.ZERO:
		velocity.x = direction.x * SPEED
		velocity.z = direction.z * SPEED

		# Rotate model ONLY while moving: face movement direction (keys)
		var targetYaw: float = atan2(-direction.x, -direction.z)
		model.rotation.y = lerp_angle(model.rotation.y, targetYaw, TURN_SPEED * delta)
	else:
		velocity.x = move_toward(velocity.x, 0.0, SPEED)
		velocity.z = move_toward(velocity.z, 0.0, SPEED)

	move_and_slide()
